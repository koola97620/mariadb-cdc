plugins {
    id 'java-library'
    id 'maven-publish'
    id 'java-library-distribution'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

group = "com.github.madvirus"
version = '1.0.4.1'

repositories {
    mavenCentral()
}

dependencies {
    implementation("com.zendesk:mysql-binlog-connector-java:0.23.2")
    implementation("org.mariadb.jdbc:mariadb-java-client:3.1.4")
    implementation("org.slf4j:slf4j-api:1.7.30")

    testImplementation 'ch.qos.logback:logback-classic:1.4.14'
    testImplementation(platform('org.junit:junit-bom:5.10.0'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation("org.assertj:assertj-core:3.24.2")
    testImplementation("org.mockito:mockito-core:5.8.0")
    testImplementation("org.testcontainers:testcontainers:1.19.3")
    testImplementation("org.testcontainers:mariadb:1.18.3")
    testImplementation "org.testcontainers:junit-jupiter:1.19.3"
}

def test = tasks.named("test") {
    useJUnitPlatform {
        excludeTags "integration"
    }
}

def integrationTest = tasks.register("integrationTest", Test) {
    useJUnitPlatform {
        includeTags "integration"
    }
    shouldRunAfter test
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
